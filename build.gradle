buildscript {
    repositories {
        jcenter()
        google()
        mavenCentral()
    }
    dependencies {
//        classpath 'com.android.tools.build:gradle:3.2.0-alpha12'
        classpath 'com.android.tools.build:gradle:3.2.0'//add by jack
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.6'
//        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
    }
}


final String SUPPORT_LIBS_VERSION = '28.0.0'

apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'
apply from: 'versions.gradle'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'

    defaultConfig {
//        applicationId 'com.mango.launcher3' //add by jack
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 900 //1 modify by jack
        versionName "9.0" //1.0 modify by jack

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        debug {
            storeFile file('./mango-launcher3.jks')
            storePassword "mangolauncher3"
            keyAlias "Launcher3"
            keyPassword "mangolauncher3"
        }

        release {
            storeFile file('./mango-launcher3.jks')
            storePassword "mangolauncher3"
            keyAlias "Launcher3"
            keyPassword "mangolauncher3"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }

//        release {
//            minifyEnabled false
//            signingConfig signingConfigs.release
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "default"

    productFlavors {
        mango {
            dimension "default"
            applicationId 'com.mango.launcher3'
            testApplicationId 'com.mango.launcher3.tests'
        }

        aosp {
            dimension "default"
            applicationId 'com.android.launcher3'
            testApplicationId 'com.android.launcher3.tests'
        }

        l3go {
            dimension "default"
            applicationId 'com.android.launcher3'
            testApplicationId 'com.android.launcher3.tests'
        }

        quickstep {
            dimension "default"
            applicationId 'com.android.launcher3'
            testApplicationId 'com.android.launcher3.tests'
        }
    }

    // Disable release builds for now
    android.variantFilter { variant ->
        if (variant.buildType.name.endsWith('release')) {
            variant.setIgnore(true);
        }
    }

    sourceSets {
        main {
            res.srcDirs = ['res']
            java.srcDirs = ['src']
            manifest.srcFile 'AndroidManifest-common.xml'
            proto {
                srcDir 'protos/'
                srcDir 'proto_overrides/'
            }
        }

        debug {
            manifest.srcFile "AndroidManifest.xml"
        }

        androidTest {
            res.srcDirs = ['tests/res']
            java.srcDirs = ['tests/src']
            manifest.srcFile "tests/AndroidManifest-common.xml"
        }

        androidTestDebug {
            manifest.srcFile "tests/AndroidManifest.xml"
        }

        mango {
            java.srcDirs = ['src_flags', "src_ui_overrides", "oauth/src"]
        }

        aosp {
            java.srcDirs = ['src_flags', "src_ui_overrides"]
        }

        l3go {
            res.srcDirs = ['go/res']
            java.srcDirs = ['go/src_flags', "src_ui_overrides"]
            manifest.srcFile "go/AndroidManifest.xml"
        }

        quickstep {
            res.srcDirs = ['quickstep/res']
            java.srcDirs = ['src_flags', 'quickstep/src']
            manifest.srcFile "quickstep/AndroidManifest.xml"
        }
    }
}

repositories {
    maven { url "../../../prebuilts/fullsdk-darwin/extras/android/m2repository" }
    maven { url "../../../prebuilts/fullsdk-linux/extras/android/m2repository" }
    mavenCentral()
    google()
}
def retrofit = "2.4.0"

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'org.mockito:mockito-core:1.9.5'
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.0'
    androidTestImplementation 'com.android.support.test:rules:1.0.0'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestImplementation "com.android.support:support-annotations:${SUPPORT_LIBS_VERSION}"

    implementation "com.android.support:design:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:support-v4:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:support-dynamic-animation:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:recyclerview-v7:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:appcompat-v7:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:preference-v7:${SUPPORT_LIBS_VERSION}"
    implementation 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-7'
    implementation 'com.google.guava:guava:26.0-android'
    implementation 'com.amap.api:location:latest.integration'

    implementation "com.squareup.retrofit2:retrofit:${retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava:${retrofit}"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    quickstepImplementation fileTree(dir: "quickstep/libs", include: 'sysui_shared.jar')
    implementation files('libs/wechat-sdk-android-with-mta-5.1.6.jar')
}

protobuf {
    // Configure the protoc executable
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'

        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    remove java
                    javanano {
                        option "java_package=launcher_log_extension.proto|com.android.launcher3.userevent.nano"
                        option "java_package=launcher_log.proto|com.android.launcher3.userevent.nano"
                        option "java_package=launcher_dump.proto|com.android.launcher3.model.nano"
                        option "enum_style=java"
                    }
                }
            }
        }
    }
}
